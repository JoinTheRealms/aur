# $Id: PKGBUILD 200037 2013-11-21 23:30:26Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

_pkgname=libnm-qt
pkgname=${_pkgname}-no-mm
pkgver=0.9.8.0
pkgrel=1.1
pkgdesc='Qt-only wrapper for NetworkManager DBus API (without ModemManager dependency)'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
provides=("$_pkgname")
conflicts=("$_pkgname")
depends=('networkmanager')
makedepends=('cmake' 'doxygen')
source=("http://download.kde.org/unstable/networkmanager-qt/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('02242d49958ac88e056f093cff08f9e94ff888d14a6765180ddc991caceb5db8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DDISABLE_MODEMMANAGERQT=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
