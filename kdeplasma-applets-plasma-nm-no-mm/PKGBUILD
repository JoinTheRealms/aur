# $Id: PKGBUILD 200039 2013-11-22 00:08:52Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas < nqn1976 @ gmail.com >

_pkgname=kdeplasma-applets-plasma-nm
pkgname=${_pkgname}-no-mm
pkgver=0.9.3.2
pkgrel=1.1
pkgdesc="Plasma applet written in QML for managing network connections (without ModemManager support)"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/network/plasma-nm"
license=('GPL')
depends=('kdebase-workspace' 'libnm-qt')
makedepends=('cmake' 'automoc4')
conflicts=("$_pkgname" "kdeplasma-applets-networkmanagement")
provides=("$_pkgname")
source=("http://download.kde.org/unstable/plasma-nm/plasma-nm-${pkgver}.tar.xz")
md5sums=('67dde011a859f2d72633cf3af5c9f847')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../plasma-nm-${pkgver} \
    -DDISABLE_MODEMMANAGER_SUPPORT=1 \
    -DWITH_MobileBroadbandProviderInfo=0 \
    -DWITH_OpenConnect=0 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
