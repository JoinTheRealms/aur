# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Yonathan Dossow
# Contributor: Ansgar Taflinski <ataflinski@uni-koblenz.de>
# Contributor: Peter Wu <peter@lekensteyn.nl>

pkgname=xf86-video-qxl
pkgver=0.1.4
pkgrel=2
pkgdesc='Xorg X11 qxl video driver'
arch=('x86_64' 'i686')
url='http://www.x.org'
license=('MIT')
groups=('xorg-drivers')
depends=('spice' 'libsystemd')
optdepends=('python2: for Xspice')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=19' 'spice-protocol>=0.12.9'
             'randrproto' 'renderproto' 'videoproto' 'xf86dgaproto'
             'xproto' 'fontsproto')
conflicts=('xf86-video-qxl-git' 'X-ABI-VIDEODRV_VERSION<19' 'X-ABI-VIDEODRV_VERSION>=20')
validpgpkeys=('94A9F75661F77A6168649B23A9D8C21429AC6C82')
source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"{,.sig})

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  sed -i '1c #!/usr/bin/python2' scripts/Xspice
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  # https://bugs.freedesktop.org/show_bug.cgi?id=72169
  if [[ $CARCH == x86_64 ]]; then
    _enable_xspice=--enable-xspice
  else
    _enable_xspice=
  fi

  ./configure --prefix=/usr $_enable_xspice
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"

  make DESTDIR="${pkgdir}/" install
  install -m644 -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('e57ad22f0b1daf8979e722be0cc38d351bfcc2e41568edc566d2bade5a621105'
            'SKIP')
