# $Id: PKGBUILD 194226 2013-09-11 02:06:50Z eric $
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=('wireshark-cli' 'wireshark-gtk')
pkgbase=wireshark
pkgver=1.11.2.r54464
# tarball is named as such
_pkgver=1.11.3
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('cmake' 'gtk2' 'krb5' 'libpcap' 'bash' 'gnutls'
             'lua' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
url='http://www.wireshark.org/'
options=(!libtool)
source=(${pkgbase}-${_pkgver}.tar.bz2)
sha256sums=('SKIP')

prepare() {
  cd ${pkgbase}-${_pkgver}

  # Prevent using absolute cmake build path for documentation CSS
  sed 's#--css=.*/docbook/ws.css#--css=ws.css#' \
    -i cmake/modules/FindPOD.cmake
}

build() {
  cd ${pkgbase}-${_pkgver}

  mkdir -p build
  cd build

  # QT5 is broken, "Unknown CMake command "QT_ADD_TRANSLATION"
  cmake \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	-DENABLE_GNUTLS=1 \
	-DENABLE_ZLIB=1 \
	-DENABLE_LUA=1 \
	-DENABLE_GTK3=0 \
	\
	-DENABLE_QT5=0 \
	-DENABLE_GEOIP=0 \
	..
  make all
}

package_wireshark-cli() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('krb5' 'libpcap' 'bash' 'gnutls'  'glib2' 'lua')
  install=wireshark.install
  conflicts=(wireshark)

  cd ${pkgbase}-${_pkgver}

  make -C build DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark"

  # Headers
  install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h build/config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
  for d in epan wiretap; do
    install -m644 build/${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-gtk() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('gtk2' 'wireshark-cli' 'desktop-file-utils' 'hicolor-icon-theme')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)

  cd ${pkgbase}-${_pkgver}

  install -Dm755 build/run/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
