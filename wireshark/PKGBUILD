# Maintainer: Peter Wu <peter@lekensteyn.nl>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>

# Development snapshot using wireshark-gtk as default 'wireshark' program.

pkgname=('wireshark-cli' 'wireshark-gtk' 'wireshark-qt')
pkgbase=wireshark
pkgver=1.99.1rc0_232_g5e4e17c
# autofoo tarball has only the abbrev. name, but ./tools/git-export-release.sh
# creates a tarball with the full git version.
_pkgver=${pkgver//_/-}
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('cmake' 'gtk2' 'qt5-tools'
             'krb5' 'libpcap' 'libgcrypt' 'gnutls' 'portaudio'
             'lua' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
url='https://www.wireshark.org/'
options=(!libtool)
source=(${pkgbase}-${_pkgver}.tar.xz)
sha256sums=('SKIP')

prepare() {
  cd ${pkgbase}-${_pkgver}

  # Prevent using absolute cmake build path for documentation CSS
  sed 's#--css=.*/docbook/ws.css#--css=ws.css#' \
    -i cmake/modules/FindPOD.cmake
}

build() {
  cd ${pkgbase}-${_pkgver}

  mkdir -p build
  cd build

  # cares needs c-ares package
  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_SKIP_RPATH=1 \
      -DENABLE_CARES=0 \
      -DENABLE_GCRYPT=1 \
      -DENABLE_GEOIP=0 \
      -DENABLE_GNUTLS=1 \
      -DENABLE_GTK3=0 \
      -DENABLE_KERBEROS=1 \
      -DENABLE_LUA=1 \
      -DENABLE_PORTAUDIO=1 \
      -DENABLE_SBC=0 \
      -DENABLE_SMI=0 \
      -DENABLE_QT5=1 \
      -DENABLE_ZLIB=1 \
      ..
  make all
}

package_wireshark-cli() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('krb5' 'libgcrypt' 'libcap' 'libpcap' 'gnutls'  'glib2' 'lua')
  install=wireshark.install
  conflicts=(wireshark)

  cd ${pkgbase}-${_pkgver}

  make -C build DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm \
    "${pkgdir}/usr/bin/wireshark-gtk" \
    "${pkgdir}/usr/bin/wireshark"

  # Headers
  install -dm755 "${pkgdir}"/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h build/config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
  for d in epan wiretap; do
    install -m644 build/${d}/*.h "${pkgdir}"/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-gtk() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('gtk2' 'portaudio' 'wireshark-cli' 'desktop-file-utils' 'hicolor-icon-theme')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)

  cd ${pkgbase}-${_pkgver}

  # Since 1.99, wireshark is not the gtk version anymore
  install -Dm755 build/run/wireshark-gtk "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}

package_wireshark-qt() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - Qt frontend'
  depends=('qt5-base' 'wireshark-cli' 'desktop-file-utils')
  install=wireshark-qt.install

  cd ${pkgbase}-${_pkgver}
  # Since 1.99, wireshark-qt is the default wireshark
  install -Dm755 build/run/wireshark "${pkgdir}/usr/bin/wireshark-qt"

  install -d "${pkgdir}/usr/share/applications"
  sed -e 's/^Name.*=Wireshark/& (QT)/g' -e '/Exec=/s/wireshark/&-qt/g' \
    wireshark.desktop > "${pkgdir}/usr/share/applications/wireshark-qt.desktop"
}
