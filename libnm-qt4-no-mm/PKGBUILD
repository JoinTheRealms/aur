# $Id: PKGBUILD 225796 2014-11-09 10:32:47Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libnm-qt4-no-mm
groups=('modified')
pkgver=0.9.8.4
pkgrel=1
pkgdesc='Qt4-only wrapper for NetworkManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/networkmanager-qt'
license=('LGPL')
depends=('qt4' 'networkmanager')
conflicts=('libnm-qt' 'libnm-qt4')
provides=('libnm-qt' 'libnm-qt4')
makedepends=('cmake' 'doxygen')
source=("http://download.kde.org/unstable/networkmanager-qt/${pkgver}/src/networkmanager-qt-${pkgver}.tar.xz")
sha256sums=('3c6758da6c12668198ff8fdeda2f04c7b620f2ef1a506a1bdaeda63c06fbaff4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../networkmanager-qt-${pkgver} \
    -DDISABLE_MODEMMANAGERQT=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DDISABLE_TESTING=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
