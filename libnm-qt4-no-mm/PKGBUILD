# $Id: PKGBUILD 225796 2014-11-09 10:32:47Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libnm-qt4-no-mm
groups=('modified')
pkgver=0.9.8.3
pkgrel=2
pkgdesc='Qt4-only wrapper for NetworkManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/networkmanager-qt'
license=('LGPL')
depends=('qt4' 'networkmanager')
conflicts=('libnm-qt' 'libnm-qt4')
provides=('libnm-qt' 'libnm-qt4')
makedepends=('cmake' 'doxygen')
source=("http://download.kde.org/unstable/networkmanager-qt/${pkgver}/src/libnm-qt-${pkgver}.tar.xz"
        "0001-Checking-for-security-in-wireless-setting-won-t-work.patch")
# Apparently there have been multiple "0.9.8.3"s. The listed one has included
# Doxyfile patches (compared against the v0.9.8.3 git tag)
sha256sums=('6c74ed6cb6e96088fbee709feddd1a1da0de46b64023cde4e26c841b8171105b'
            '70243ab5eb403ca33cfedcf61b73718877d51b58cdca4cb9450eaa275c2d240f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libnm-qt-${pkgver} \
    -DDISABLE_MODEMMANAGERQT=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DDISABLE_TESTING=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
