# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=2.0.1
pkgrel=2
pkgdesc="A tool for searching a given binary image for embedded files"
arch=('any')
url="http://binwalk.org"
license=('MIT')
depends=('python')
optdepends=(
  'python-pyqtgraph: for generating graphs and visualization'
  # already satisfied by python-pyqtgraph
  #'python-opengl: for generating graphs and visualization'
  #'python-numpy: for generating graphs and visualization'

  # Optional runtime dependencies
  # already satisfied by python:
  #'bzip2: bzip2 decompression support'
  # see extract.conf and plugins/
  'arj: ARJ decompression support'
  'cabextract: CAB archive support'
  'cpio: CPIO archvie support'
  'gzip: GZIP decompression support'
  'lha: LHA decompression support'
  'mtd-utils: JFFS filesystem support'
  'p7zip: 7z, LZMA and iso decompression support'
  'squashfs-tools: squashfs support'
  'tar: TAR archive support'
  'unrar: RAR decompression support'
  'xz: XZ-compressed tar archive support'
  'firmware-mod-kit: cramfs support'
  #'java-environment: no idea why it is suggested'
)
source=(https://github.com/devttys0/${pkgname}/archive/v${pkgver}.tar.gz)
md5sums=('ffeea02b765e44ac7a2455398011fc73')
sha256sums=('90ee8426d71e91b62dfe4a1446c457bc7835b475b28717859e275a0494403959')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python ./setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python ./setup.py install --prefix="${pkgdir}/usr"
  install -m 644 -D "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
