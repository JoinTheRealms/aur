# Maintainer: Peter Wu <peter@lekensteyn.nl>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=('wireshark-cli-git' 'wireshark-gtk-git' 'wireshark-qt-git')
pkgbase=wireshark-git
pkgver=1.99.3rc0.r349.g9c55903
pkgrel=1.1
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('git' 'cmake' 'ninja'
             'gtk3' 'qt5-tools'
             'krb5' 'libpcap' 'libgcrypt' 'gnutls' 'portaudio'
             'lua' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
url='https://www.wireshark.org/'
_gitname=wireshark
source=(git+https://code.wireshark.org/review/wireshark)
sha1sums=('SKIP')
replaces=('wireshark-cli' 'wireshark-gtk' 'wireshark-qt')
provides=('wireshark-cli' 'wireshark-gtk' 'wireshark-qt')

pkgver() {
  cd "${srcdir}/$_gitname"
    git describe --match=v\* | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/$_gitname"

  mkdir -p build
  cd build

  # cares needs c-ares package
  cmake \
      -G Ninja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_SKIP_RPATH=1 \
      -DENABLE_CARES=0 \
      -DENABLE_GCRYPT=1 \
      -DENABLE_GEOIP=1 \
      -DENABLE_GNUTLS=1 \
      -DENABLE_GTK3=1 \
      -DENABLE_KERBEROS=1 \
      -DENABLE_LUA=1 \
      -DENABLE_PORTAUDIO=1 \
      -DENABLE_SBC=0 \
      -DENABLE_SMI=0 \
      -DENABLE_QT5=1 \
      -DENABLE_ZLIB=1 \
      -DCMAKE_BUILD_TYPE=RelWithDebInfo \
      -DDISABLE_WERROR=1 \
      -DCMAKE_C_FLAGS="$CFLAGS" \
      -DCMAKE_CXX_FLAGS="$CXXFLAGS" \
      ..
  ninja
}

package_wireshark-cli-git() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('krb5' 'libgcrypt' 'libcap' 'libpcap' 'geoip' 'gnutls' 'glib2' 'lua')
  install=wireshark.install
  conflicts=(wireshark)

  cd "${srcdir}/${_gitname}"

  DESTDIR="${pkgdir}" ninja -C build install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm \
    "${pkgdir}/usr/bin/wireshark-gtk" \
    "${pkgdir}/usr/bin/wireshark"

  # Headers
  install -dm755 "${pkgdir}"/usr/include/wireshark/{epan/{crypt,dfilter,dissectors,ftypes,wmem},wiretap,wsutil}

  install -m644 color.h build/config.h register.h ws_symbol_export.h "${pkgdir}/usr/include/wireshark/"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes epan/wmem wiretap wsutil; do
    install -m644 ${d}/*.h "${pkgdir}"/usr/include/wireshark/${d}
  done
  for d in epan wiretap; do
    install -m644 build/${d}/*.h "${pkgdir}"/usr/include/wireshark/${d}
  done
}

package_wireshark-gtk-git() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('gtk3' 'portaudio' 'wireshark-cli' 'desktop-file-utils' 'hicolor-icon-theme')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)

  cd "${srcdir}/${_gitname}"

  install -Dm755 build/run/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}

package_wireshark-qt-git() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - Qt frontend'
  depends=('qt5-base' 'wireshark-cli-git' 'desktop-file-utils')
  install=wireshark-qt.install

  cd "${srcdir}/${_gitname}"
  install -Dm755 build/run/wireshark "${pkgdir}/usr/bin/wireshark"

  install -d "${pkgdir}/usr/share/applications"
  sed -e 's/^Name.*=Wireshark/& (QT)/g' -e '/Exec=/s/wireshark/&-qt/g' \
    wireshark.desktop > "${pkgdir}/usr/share/applications/wireshark-qt.desktop"
}
