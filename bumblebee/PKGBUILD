# This script is incomplete and in development.

# Maintainer: Samsagax <samsagax@gmail.com>

pkgname=bumblebee
pkgver=3.0
pkgrel=3
pkgdesc="Bumblebee brings Optimus Support for Linux Through VirtualGL. You need to install proper drivers separately. Can be used with Nouveau or Nvidia"
arch=('i686' 'x86_64')
makedepends=('help2man')
depends=('virtualgl' 'libbsd' 'glib2')
optdepends=('xf86-video-nouveau: Nouveau driver' 'nouveau-dri: 3D acceleration features fo Nouveau' 'mesa: 3D acceleration features fo Nouveau'  'bbswitch: switch on/off discrete card' 'nvidia-utils-bumblebee: Nvidia utils not breaking LibGL' 'nvidia: Nvidia kernel driver')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi
url="http://www.Bumblebee-Project.org"
license=("GPL3")
install='bumblebee.install'
provides=('bumblebee')
backup=('etc/bumblebee/bumblebee.conf')
_patches=("001-no-set-ldpath.patch" "002-ignore-empty-kerneldriver.patch"
  "003-add-pm-method-option.patch" "004-no-crash-on-invalid-pm-method.patch"
  "005-remove-switcheroo-restriction.patch" "006-set-vgl-readback-pbo.patch")
_patchdir="https://raw.github.com/Bumblebee-Project/bumblebee-ppa/master/precise/bumblebee/debian/patches"
source=("https://github.com/downloads/Bumblebee-Project/Bumblebee/${pkgname}-${pkgver}.tar.gz" 'bumblebeed.in'
  "$_patchdir/001-no-set-ldpath.patch"
  "$_patchdir/002-ignore-empty-kerneldriver.patch"
  "$_patchdir/003-add-pm-method-option.patch"
  "$_patchdir/004-no-crash-on-invalid-pm-method.patch"
  "$_patchdir/005-remove-switcheroo-restriction.patch"
  "$_patchdir/006-set-vgl-readback-pbo.patch")
md5sums=('2ddd4d47bc52aceb3b295e68f9f32ffe'
         '8b05da760e9236ecff2c6820616119f7'
         'cb751c49d273c4ba2f080a474fa2ae7d'
         '6e0d7b03d3f6d53f3530a82721007892'
         'ddaa9173cef67e29504b61c7b8a3bae7'
         '1a42cc333679dec19b7db2fbec5f4d65'
         '1a8d0dc34a12f16c44cb96081970ff89'
         '37ee4dabf0b79f08e2284bfed2a1d3ec')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    for i in "${_patches[@]}"; do
      patch -p1 < "${srcdir}/${i}"
    done
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia-bumblebee:/usr/lib32/nvidia-bumblebee \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia-bumblebee/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make install DESTDIR="$pkgdir"
    install -D -m755 "$srcdir/bumblebeed.in" "$pkgdir/etc/rc.d/bumblebeed"
    # Make bash_completion work
    mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
}
