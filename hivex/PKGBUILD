# Contributor: Peter Wu <peter@lekensteyn.nl>
# Maintainer: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=hivex
pkgver=1.3.10
pkgrel=1
pkgdesc="System for extracting the contents of Windows Registry."
arch=("i686" "x86_64")
url="http://libguestfs.org"
license=("LGPL2.1")
_pythonver=3
depends=(
    "libxml2"
    "perl"
    )
# If you build from git, you also need the ocaml packages below. In that case,
# you can still disable the OCaml feature with --disable-ocaml.
makedepends=(
#    "ocaml-findlib"
#    "ocaml"
    "perl-io-stringy"
    "perl-test-simple"
    "python$_pythonver"
#    "ruby"
    )
options=("!emptydirs" "!libtool")
source=("http://libguestfs.org/download/$pkgname/$pkgname-$pkgver.tar.gz"
        "http://libguestfs.org/download/$pkgname/$pkgname-$pkgver.tar.gz.sig")

build() {
    cd "$srcdir/$pkgname-$pkgver"
    unset PREFIX
    # Some language bindings are disabled. If you need to build applications in
    # those language, remove --disable-* below and uncomment the packages in
    # makedepends.
    PYTHON=python$_pythonver \
    ./configure \
        --prefix=/usr \
        --bindir=/usr/bin \
        --libdir=/usr/lib \
        --disable-static \
        --disable-rpath \
        --enable-perl \
        --enable-python \
        --disable-ocaml \
        --disable-ruby
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
md5sums=('38f82c568e71a9783b12e1983fdf71f9'
         'SKIP')
