# $Id: PKGBUILD 69758 2012-04-23 09:15:08Z ibiru $
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: garion < garion @ mailoo.org >
# Contributor: leemars

pkgname=ibus-qt
pkgver=1.3.1
pkgrel=6
pkgdesc='IBus qt library and IBus qt input method plugin'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('qt' 'ibus' 'icu')
makedepends=('cmake' 'automoc4' 'doxygen')
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz"
	"0001-Do-not-crash-on-missing-invalid-DISPLAY-envvar.patch")
md5sums=('769e8872ca8a59327b2073ce2f142589'
	 'e91f915c46d84ebacdd1c526a791d1ec')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"

  patch -Np1 -i "${srcdir}/0001-Do-not-crash-on-missing-invalid-DISPLAY-envvar.patch"

  mkdir -p build
  cd build
  
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source/build"
  make DESTDIR="${pkgdir}" install
}
